(load "sdl.carp")
(load "sdl_image.carp")
(load "Vector.carp")

(use-all Keycode Vector2)

      ;(SDL_SetRenderDrawBlendMode rend SDL_BLENDMODE_ADD)
(defmodule Anima
  (def fr 60)

  ; super naive way
  (defn framerate [n]
    (set! &fr n))

  (defn set-color [app rend r g b alpha]
    (SDL_SetRenderDrawColor rend r g b alpha))

  (defn-do set-background [app rend r g b alpha]
    (SDL_SetRenderDrawColor rend r g b alpha)
    (SDL_RenderClear rend))

  (defn line [app rend ax ay bx by]
    (SDL_RenderDrawLine rend ax ay bx by))

  (defn random [i]
    (Int.random-between 0 i))

  (defn random-between [min max]
    (Int.random-between min max))

  (defn rect [app rend ax ay w h]
    (let [bx ax
          by ay
          r (make-rect bx by (- w bx) (- h by))]
      (SDL_RenderFillRect rend (address r))))

  (defn cvrt- [c]
    (Float.to-int (+ (* c 255.0f) 0.5f)))

  (defn hsb-to-rgb [ih is ib]
    (let [h (/ (Float.from-int ih) 360.0f)
          s (/ (Float.from-int is) 100.0f)
          b (/ (Float.from-int ib) 100.0f)
          i (cvrt- b)]
      (if (= 0.0f s)
        [i i i]
        (let [h1 (* (- h (Float.floor h)) 6.0f)
              f (- h1 (Float.floor h1))
              p (* b (- 1.0f s))
              q (* b (- 1.0f (* s f)))
              t (* b (- 1.0f (* s (- 1.0f f))))]
          (case (Float.to-int h1)
            0 [i (cvrt- t) (cvrt- p)]
            1 [(cvrt- q) (cvrt- b) (cvrt- p)]
            2 [(cvrt- p) (cvrt- b) (cvrt- t)]
            3 [(cvrt- p) (cvrt- q) (cvrt- b)]
            4 [(cvrt- t) (cvrt- p) (cvrt- b)]
            5 [(cvrt- b) (cvrt- p) (cvrt- q)]
            [])))))

  (defn handle-events [app rend]
    (let [event (SDL_Event_init)]
      (while (SDL_PollEvent (address event))
        (let [et (event-type &event)]
          (cond (= et SDL_QUIT) (quit &app)
                (= et SDL_KEYDOWN) (let [key (event-keycode &event)]
                                     (when (= key SDLK_ESCAPE) (quit &app)))
                ())))))
)

(defmacro defsketch [name w h setup :rest draw-and-state]
  (if (< 2 (count draw-and-state))
    (macro-error "Cannot define sketch with more than six arguments.")
    (list 'defn 'main (array)
      (list 'let (array 'app (list 'app-init (list 'copy name) w h)
                        'rend (list 'app-renderer 'app)
                        'anima--state (if (= (count draw-and-state) 2)
                                        (list (car (cdr draw-and-state)))
                                        0)) ;; TODO: remove standin
        (list 'do
          (list 'System.srand (list 'System.time))
          (if (> (count draw-and-state) 0) (list setup 'app 'rend) (list))
          (list 'while true
            (list 'do
                  (list 'handle-events 'app 'rend)
                  (if (> (count draw-and-state) 0)
                    (if (> (count draw-and-state) 1)
                      (list 'set! (list 'ref 'anima--state)
                            (list (car draw-and-state) 'app 'rend (list 'copy (list 'ref 'anima--state))))
                      (list (car draw-and-state) 'app 'rend))
                    (list setup 'app 'rend))
                  (list 'SDL_RenderPresent 'rend)
                  (list 'if (list '= 'fr 0)
                    (list 'forever-do (list 'handle-events 'app 'rend))
                    (list 'SDL_Delay (list '/ 1000 'fr))))))))))

(defdynamic setter [fn a r color cs]
  (if (= (count cs) 0)
    (list fn a r color color color 255)
    (if (= (count cs) 2)
      (list fn a r color (car cs) (car (cdr cs)) 255)
      (list fn a r color (car cs) (car (cdr cs)) (car (cdr (cdr cs)))))))

(defmacro color [a r color :rest cs]
  (setter 'set-color a r color cs))

(defmacro background [a r background :rest cs]
  (setter 'set-background a r background cs))
